apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


version = rootProject.versionName
group 'com.ncl'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def bintrayUser = properties.getProperty('bintrayUser')
def bintrayKey = properties.getProperty('bintrayKey')


publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/ncl-debug-release.aar")
            groupId
            artifactId 'ncl-debug'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')

                        String dependencyGroup = it.group
                        String dependencyVersion = it.version

                        // When depending on a local project, their version comes as 'unspecified'
                        // We will use this fact to intercept those dependencies and set group and version.
                        if (dependencyVersion.equals('unspecified')) {
                            dependencyVersion = rootProject.versionName
                            dependencyGroup = this.group
                        }

                        dependencyNode.appendNode('groupId', dependencyGroup)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', dependencyVersion)
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'ncl-debug'
        description = 'A debug plugin implementation for the NCL Plugin infrastructure.'
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pablichjenkov/PluginManager.git'
        dryRun = true
        version {
            name = this.version
            desc = "aar for version: ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}
